plugins {
    id 'eclipse'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = mod_version
group = mod_group_id
base {
    archivesName = mod_id
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: mapping_channel, version: mapping_version
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    copyIdeResources = true
    runs {
        configureEach {
            // 为 JetBrain Runtime 添加的参数，方便热重载
            jvmArgs = [
                    "-XX:+IgnoreUnrecognizedVMOptions",
                    "-XX:+AllowEnhancedClassRedefinition"
            ]
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            // 控制台彩色打印
            property 'terminal.jline', 'true'
            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'forge.enabledGameTestNamespaces', mod_id
            workingDirectory project.file('run/client')
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            workingDirectory project.file('run/server')
            args '--nogui'
        }

        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
            workingDirectory project.file('run/gametest')
        }

        data {
            workingDirectory project.file('run/data')
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
    // 排除 datagen 生成的缓存文件
    exclude '.cache/**'
}

repositories {
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven { // Registrate
        url "https://maven.tterrag.com/"
    }
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
        content {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = 'sponge-repo'
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroup("software.bernie.geckolib")
            includeGroup("com.eliotlash.mclib")
        }
    }
    flatDir {
        dirs "libs", "TouhouLittleMaid/libs"
    }
    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    implementation fg.deobf("curse.maven:touhou-little-maid-355044:7510714-sources-7510716")

    implementation fg.deobf("curse.maven:applied-energistics-2-223794:7148487")
    implementation fg.deobf("curse.maven:guideme-1173950:7127447")


    runtimeOnly fg.deobf("maven.modrinth:caelus:3.2.0+1.20.1-forge")
    runtimeOnly fg.deobf("maven.modrinth:playeranimator:1.0.2-rc1+1.20-forge")
    //implementation fg.deobf("maven.modrinth:irons-spells-n-spellbooks:1.20.1-3.4.0.9-forge")
    implementation fg.deobf("curse.maven:irons-spells-n-spellbooks-855414:7402504")

    implementation fg.deobf("maven.modrinth:ars-nouveau:4.12.6-forge,1.20.1")

    runtimeOnly fg.deobf("maven.modrinth:curios:5.14.1+1.20.1-forge")
    //implementation fg.deobf("maven.modrinth:goety:2.5.34.3-forge,1.20.1")3
    implementation fg.deobf("maven.modrinth:goety:2.5.37.0-forge,1.20.1")

    runtimeOnly fg.deobf("maven.modrinth:patchouli:1.20.1-84.1-forge")
    implementation fg.deobf("maven.modrinth:psi:1.20.1-1.20.1-102-forge")

    implementation fg.deobf("maven.modrinth:youkaishomecoming:2.6.1-forge,1.20.1")
    implementation fg.deobf("maven.modrinth:farmers-delight:1.20.1-1.2.9-forge")
    implementation fg.deobf("maven.modrinth:l2library:2.5.3-forge,1.20.1")
    compileOnly fg.deobf(jarJar("com.tterrag.registrate:Registrate:${registrate_version}")) {
        transitive = false
        jarJar.ranged(it, "[MC1.20-1.3.3,)")
    }

    implementation fg.deobf("maven.modrinth:slashblade-resharped:1.6.54-forge,1.20.1")

    runtimeOnly fg.deobf("curse.maven:jei-238222:7037120")
    runtimeOnly fg.deobf("curse.maven:mouse-tweaks-60089:5338457")
    runtimeOnly fg.deobf("curse.maven:imblocker-483760:6487340")
    runtimeOnly fg.deobf("maven.modrinth:jade:11.13.2+forge")
    runtimeOnly fg.deobf("maven.modrinth:justenoughcharacters:4.5.16-forge,1.20.1")

    // Geckolib 动画
    implementation fg.deobf("software.bernie.geckolib:geckolib-forge-${minecraft_version}:${geckolib_version}")
    implementation("com.eliotlash.mclib:mclib:20")
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : mod_version,
            mod_authors            : mod_authors,
            mod_description        : mod_description,
    ]
    inputs.properties replaceProperties
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

tasks.named('jar', Jar).configure {
    duplicatesStrategy = DuplicatesStrategy.WARN

    manifest {
        attributes([
                "Implementation-Title"  : project.name,
                "Implementation-Version": "${version}"
        ])
    }
    finalizedBy 'reobfJar'
}

tasks.withType(JavaCompile).configureEach {
    // 强制使用 UTF-8 编码，避免中文乱码
    options.encoding = 'UTF-8'
    // 忽略过时API的警告
    options.compilerArgs += ['-Xlint:-removal']
}

mixin {
    add sourceSets.main, "maidspell.refmap.json"
    config "maidspell.mixins.json"

    // 确保 refmap 文件被正确处理
    dumpTargetOnFailure true
}

// 让 IDEA 强制下载前置库的源代码，方便开发时查看
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
